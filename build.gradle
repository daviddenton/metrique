
buildscript {
    repositories {
        mavenCentral()
        jcenter()
    }

    dependencies {
        classpath 'net.saliman:gradle-cobertura-plugin:2.0.0' // cobertura plugin
        classpath 'org.kt3k.gradle.plugin:coveralls-gradle-plugin:2.3.1'
    }
}

plugins {
    id "com.jfrog.bintray" version "1.1"
    id 'net.saliman.cobertura' version '2.2.5'
    id 'com.github.kt3k.coveralls' version '2.0.1'
}

repositories {
    mavenCentral()
    jcenter()
}

apply plugin: 'cobertura'
apply plugin: 'com.github.kt3k.coveralls'
apply plugin: 'java'
apply plugin: 'com.jfrog.bintray'
apply plugin: 'maven-publish'

cobertura.coverageFormats = ['html', 'xml']


archivesBaseName = 'metrique'

sourceCompatibility = JavaVersion.VERSION_1_7
targetCompatibility = JavaVersion.VERSION_1_7

version = project.hasProperty('releaseVersion') ? project.releaseVersion : 'LOCAL'
group = 'io.github.daviddenton'

description = 'Hierarchical metrics sent to StatsD.'

dependencies {
    compile 'com.google.guava:guava:18.0'
    compile 'io.dropwizard.metrics:metrics-core:3.1.1'
    compile 'com.readytalk:metrics3-statsd:4.1.0'
    testCompile 'junit:junit:4.12'
    testCompile 'org.hamcrest:hamcrest-all:1.3'
    testCompile 'org.mockito:mockito-all:1.9.5'
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = 'metrique'

            pom.withXml {
                asNode().appendNode('name', 'metrique')
                asNode().appendNode('description', description)
                asNode().appendNode('url', 'http://metrique.github.io')
                asNode().appendNode('developers').appendNode('developer').
                        appendNode('name', 'David Denton').parent().
                        appendNode('email', 'mail@daviddenton.github.io')
                asNode().appendNode('scm').
                        appendNode('url', 'git@github.com:daviddenton/metrique.git').parent().
                        appendNode('connection', 'scm:git:git@github.com:daviddenton/metrique.git').parent().
                        appendNode('developerConnection', 'scm:git:git@github.com:daviddenton/metrique.git')
                asNode().appendNode('licenses').appendNode('license').
                        appendNode('name', 'Apache License, Version 2.0').parent().
                        appendNode('url', 'http://www.apache.org/licenses/LICENSE-2.0.html')
            }
            from components.java

            artifact sourcesJar
            artifact javadocJar
        }
    }
}

bintray {
    user = 'daviddenton'
    key = project.hasProperty('bintrayKey') ? project.bintrayKey : 'BINTRAYKEY'

    publications = ['mavenJava']
    dryRun = false
    publish = true
    pkg {
        repo = 'maven'
        name = 'metrique'

        desc = description
        websiteUrl = 'https://github.com/daviddenton/metrique'
        issueTrackerUrl = 'https://github.com/daviddenton/metrique/issues'
        vcsUrl = 'https://github.com/daviddenton/metrique.git'
        licenses = ['Apache-2.0']
        labels = []
        publicDownloadNumbers = true
        version {
            name = project.version
        }
    }
}
